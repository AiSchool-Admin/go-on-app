buildscript {
    ext.kotlin_version = '1.9.22'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.2.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }

    // Make flutter properties available to all subprojects
    ext {
        set('flutter.sdk', System.getenv('FLUTTER_ROOT') ?: '')
        set('flutter.compileSdkVersion', 35)
        set('flutter.targetSdkVersion', 35)
        set('flutter.minSdkVersion', 23)
        set('flutter.ndkVersion', '26.1.10909125')
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(':app')
}
subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            project.android {
                if (namespace == null) {
                    namespace = project.group.toString()
                }
                compileSdkVersion 35
            }
        }
    }

    // Force flutter extension properties for all Android library projects
    project.plugins.whenPluginAdded { plugin ->
        if (plugin.class.name == 'com.android.build.gradle.LibraryPlugin') {
            project.android {
                compileSdkVersion 35
            }
        }
    }
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
